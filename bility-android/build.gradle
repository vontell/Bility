apply plugin: 'com.android.library'

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0'
    }
}

tasks.withType(Javadoc) {
    failOnError false
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Test dependencies loaded as library deps
    api 'androidx.test:core:1.3.0'
    api 'androidx.test.uiautomator:uiautomator:2.2.0'
    api 'androidx.test:runner:1.3.0'
    api 'com.squareup.okhttp3:okhttp:3.11.0'
    api 'com.google.code.gson:gson:2.8.5'
    api 'org.michaelevans.colorart:library:0.0.3'
    api 'androidx.fragment:fragment:1.2.5'
    api project(':bility-core')
}

// Publishing to Bintray

ext {
    bintrayRepo = 'bility'
    bintrayName = 'android'

    publishedGroupId = 'org.vontech.bility'
    libraryName = 'bility-android'
    artifact = 'bility-android'

    libraryDescription = 'Android driver for the Bility framework'

    siteUrl = 'https://github.com/vontell/bility'
    gitUrl = 'https://github.com/vontell/bility.git'

    libraryVersion = '0.0.5'

    developerId = 'vontell'
    developerName = 'Aaron Vontell'
    developerEmail = 'aaron@vontech.org'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'

install.repositories.mavenInstaller.pom.withXml {
    def dependenciesNode = asNode().appendNode('dependencies')

    configurations.api.allDependencies.each {
        def dependencyNode = dependenciesNode.appendNode('dependency')
        dependencyNode.appendNode('groupId', it.group)
        dependencyNode.appendNode('artifactId', it.name)
        dependencyNode.appendNode('version', it.version)
    }
}